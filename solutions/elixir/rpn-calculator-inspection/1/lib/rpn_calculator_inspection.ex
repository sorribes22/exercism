defmodule RPNCalculatorInspection do
  def start_reliability_check(calculator, input) do
    %{input: input, pid: spawn_link(fn -> calculator.(input) end)}
  end

  def await_reliability_check_result(%{pid: pid, input: input}, results) do
    receive do
      {:EXIT, ^pid, :normal} -> Map.put(results, input, :ok)
      {:EXIT, ^pid, _reason} -> Map.put(results, input, :error)
    after
      100 -> Map.put(results, input, :timeout)
    end
  end

  def reliability_check(_, []), do: %{}
  def reliability_check(calculator, inputs) do
    original_flag = Process.flag(:trap_exit, true)

    results = inputs
    |> Enum.map(fn input -> RPNCalculatorInspection.start_reliability_check(calculator, input) end)
    |> Enum.reduce(%{}, fn proc, acc -> RPNCalculatorInspection.await_reliability_check_result(proc, acc) end)

    Process.flag(:trap_exit, original_flag)

    results
  end

  def correctness_check(calculator, inputs) do
    inputs
    |> Enum.map(fn input -> Task.async(fn -> calculator.(input) end) end)
    |> Enum.map(&Task.await(&1, 100))
  end
end
